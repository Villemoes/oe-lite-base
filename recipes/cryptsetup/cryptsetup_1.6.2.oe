SUMMARY = "cryptsetup is used to conveniently setup dm-crypt managed block devices under Linux"
DESCRIPTION = "While LUKS is a standard on-disk format, there is also a reference implementation. LUKS for dm-crypt is implemented in an enhanced version of cryptsetup."
HOMEPAGE="http://code.google.com/p/cryptsetup/"
LICENSE="GPLv2"
SRC_URI = "http://cryptsetup.googlecode.com/files/cryptsetup-1.6.2.tar.bz2"

COMPATIBLE_HOST_ARCHS = ".*linux"

RECIPE_TYPES = "machine"

inherit autotools pkgconfig auto-package-utils auto-package-libs

DEPENDS += "libuuid libdevmapper popt libgcrypt"

RECIPE_FLAGS += "cryptsetup_udev"
DEFAULT_USE_cryptsetup_udev = False
EXTRA_OECONF += "${EXTRA_OECONF_UDEV}"
EXTRA_OECONF_UDEV = "--disable-udev"
EXTRA_OECONF_UDEV:USE_cryptsetup_udev = "--enable-udev"

RECIPE_FLAGS += "cryptsetup_kernel_crypto"
DEFAULT_USE_cryptsetup_kernel_crypto = False
EXTRA_OECONF += "${EXTRA_OECONF_KERNEL_CRYPTO}"
EXTRA_OECONF_KERNEL_CRYPTO = "--disable-kernel_crypto"
EXTRA_OECONF_KERNEL_CRYPTO:USE_cryptsetup_kernel_crypto = "--enable-kernel_crypto"

RECIPE_FLAGS += "cryptsetup_selinux"
DEFAULT_USE_cryptsetup_selinux = False
EXTRA_OECONF += "${EXTRA_OECONF_SELINUX}"
EXTRA_OECONF_SELINUX = "--disable-selinux"
EXTRA_OECONF_SELINUX:USE_cryptsetup_selinux = "--enable-selinux"

AUTO_PACKAGE_UTILS = "cryptsetup veritysetup"
AUTO_PACKAGE_UTILS_RDEPENDS += "libpopt libc"

AUTO_PACKAGE_LIBS = "${PN}"
AUTO_PACKAGE_LIB_DEV_DEPENDS = "libuuid libdevmapper libgcrypt"
AUTO_PACKAGE_LIB_RDEPENDS = "libuuid libdevmapper libgcrypt libgpg-error libc"

RDEPENDS_${PN} = "${AUTO_PACKAGE_UTILS_PACKAGES} ${LIBS_AUTO_RPACKAGES}"
